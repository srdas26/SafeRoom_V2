plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
    id 'org.openjfx.javafxplugin' version '0.0.14'
}

group = 'com.saferoom'
version = '1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

def javafxVersion = '21.0.1'

dependencies {
    // JavaFX (tamamı Maven üzerinden)
    implementation "org.openjfx:javafx-controls:$javafxVersion"
    implementation "org.openjfx:javafx-fxml:$javafxVersion"
    implementation "org.openjfx:javafx-base:$javafxVersion"
    implementation "org.openjfx:javafx-graphics:$javafxVersion"
    implementation "org.openjfx:javafx-media:$javafxVersion"
    implementation "org.openjfx:javafx-web:$javafxVersion"
    implementation "org.openjfx:javafx-swing:$javafxVersion"

    // JFoenix (JavaFX UI) - Modern Material Design
    implementation 'com.jfoenix:jfoenix:9.0.10'
    
    // Ikonli (Icons for JavaFX)
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'

    // PDF rendering
    implementation 'org.apache.pdfbox:pdfbox:2.0.30'

    // Multimedia helpers
    implementation 'org.bytedeco:javacv-platform:1.5.10'

    // gRPC ve Protobuf
    implementation 'io.grpc:grpc-netty-shaded:1.58.0'
    implementation 'io.grpc:grpc-protobuf:1.58.0'
    implementation 'io.grpc:grpc-stub:1.58.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    implementation 'com.google.protobuf:protobuf-java:3.23.4'

    // Network & P2P
    implementation files('libs/jstun-0.7.4.jar')
    implementation files('libs/weupnp-0.1.4.jar')

    // Email
    implementation files('libs/javax.mail.jar')
    implementation files('libs/javax.activation-1.2.0.jar')

    // Database
    implementation files('libs/mysql-connector-j-9.2.0.jar')
    
    // SQLite for Java (pure Java, no native dependencies)
    implementation 'org.xerial:sqlite-jdbc:3.45.0.0'
    
    // JSON processing
    implementation 'com.google.code.gson:gson:2.10.1'
    
    // Crypto utilities for encryption
    implementation 'org.bouncycastle:bcprov-jdk18on:1.77'

    // Logging
    implementation files('libs/slf4j-api-1.7.36.jar')
    implementation files('libs/slf4j-simple-1.7.36.jar')

    // WebRTC - Core library (platform-independent Java code)
    implementation 'dev.onvoid.webrtc:webrtc-java:0.14.0'
    
    // WebRTC - Native libraries for all platforms
    // Gradle downloads all, JVM loads the correct one at runtime based on OS
    runtimeOnly group: 'dev.onvoid.webrtc', name: 'webrtc-java', version: '0.14.0', classifier: 'linux-x86_64'
    runtimeOnly group: 'dev.onvoid.webrtc', name: 'webrtc-java', version: '0.14.0', classifier: 'macos-x86_64'
    runtimeOnly group: 'dev.onvoid.webrtc', name: 'webrtc-java', version: '0.14.0', classifier: 'macos-aarch64'
    runtimeOnly group: 'dev.onvoid.webrtc', name: 'webrtc-java', version: '0.14.0', classifier: 'windows-x86_64'

    // JUnit 5 for testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

application {
    mainClass = 'com.saferoom.gui.MainApp'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.23.4'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.58.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = [
                'src/main/java',
                'build/generated/source/proto/main/java',
                'build/generated/source/proto/main/grpc'
            ]
        }
        proto {
            srcDirs = ['src/main/proto']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

// CompileJava task'ının generateProto'ya bağımlı olduğunu belirt
compileJava.dependsOn 'generateProto'

javafx {
    version = javafxVersion
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.base', 'javafx.media', 'javafx.web', 'javafx.swing' ]
}

// Duplicate handling strategy
processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Distribution tasks duplicate handling
tasks.withType(Tar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Zip).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += '--enable-preview'
    options.release = 21
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jvmArgs += '--enable-preview'
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += '--enable-preview'
}

